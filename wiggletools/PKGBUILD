# Maintainer: Christian Krause ("wookietreiber") <kizkizzbangbang@googlemail.com>

pkgname=wiggletools
pkgver=1.0
pkgrel=1
pkgdesc="basic operations on the space of numerical functions defined on the genome using lazy evaluators for flexibility and efficiency"
arch=('i686' 'x86_64')
url="https://github.com/Ensembl/WiggleTools"
license=('Apache')
depends=(gsl openssl)
makedepends=(git libmariadbclient libpng unzip)
checkdepends=(python2)
source=("$pkgname-$pkgver.tar.gz::https://github.com/Ensembl/WiggleTools/archive/v$pkgver.tar.gz")
md5sums=('151aa1ccfaacd77183bf2ac053ee18a0')

prepare() {
  cd $srcdir/WiggleTools-$pkgver

  git archive --format=zip -9 --remote=git://genome-source.cse.ucsc.edu/kent.git beta src/userApps > userApps.zip
  unzip -d userApps -j userApps.zip
  rm userApps.zip

  sed -e 's|python|python2|' \
      -i Makefile

  sed -e 's|-lgsl|-lgsl -lgslcblas|g' \
      -e 's|-static||g' \
      -i src/Makefile
}

build() {
  cd $srcdir/WiggleTools-$pkgver

  pushd userApps
  make fetchSource
  make
  popd

  export KENT_SRC=$PWD/userApps/kent/src
  export TABIX_SRC=$PWD/userApps/samtabix

  make
}

check() {
   cd $srcdir/WiggleTools-$pkgver

   make test
}

package() {
  cd $srcdir/WiggleTools-$pkgver

  install -Dm755 bin/wiggletools $pkgdir/usr/bin/wiggletools
}
