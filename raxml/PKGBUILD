# Maintainer: Christian Krause ("wookietreiber") <kizkizzbangbang@googlemail.com>

pkgname=raxml
pkgver=8.1.1
pkgrel=2
pkgdesc="Randomized Axelerated Maximum Likelihood"
arch=('i686' 'x86_64')
url="http://sco.h-its.org/exelixis/web/software/raxml/"
depends=('glibc')
install='raxml.install'
source=("$pkgname-$pkgver.tar.gz::https://github.com/stamatak/standard-RAxML/archive/v$pkgver.tar.gz"
        'raxml.install')
md5sums=('0be0be2edb6dce5afc0dd43a6e9b47cb'
         'SKIP')

build() {
  cd $srcdir/standard-RAxML-$pkgver

  for f in Makefile*gcc ; do
    rm -f *.o
    make -f $f || true
  done
}

package() {
  cd $srcdir/standard-RAxML-$pkgver

  for f in raxml* ; do
    install -Dm755 $f $pkgdir/usr/bin/$(basename $f)
  done

  for f in usefulScripts/* ; do
    install -Dm755 $f $pkgdir/usr/bin/$(basename $f)
  done
}
