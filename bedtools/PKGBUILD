# Maintainer: Christian Krause ("wookietreiber") <kizkizzbangbang@googlemail.com>

pkgname=bedtools
pkgver=2.23.0
pkgrel=1
pkgdesc='powerful toolset for genome arithmetic'
arch=('i686' 'x86_64')
url="https://github.com/arq5x/bedtools2"
license=('GPL2')
depends=('bash' 'zlib')
makedepends=('python2')
source=("https://github.com/arq5x/bedtools2/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('f7df3d931857f9bb8f79a69a364527d1')

prepare() {
  cd $srcdir/${pkgname}2

  sed -e 's|python|python2|' \
      -i $srcdir/${pkgname}2/Makefile
}

build() {
  cd $srcdir/${pkgname}2

  make
}

check() {
  cd $srcdir/${pkgname}2

  make test
}

package() {
  cd $srcdir/${pkgname}2

  make prefix=/usr DESTDIR=$pkgdir install
}
