# Maintainer: Christian Krause ("wookietreiber") <kizkizzbangbang@googlemail.com>

pkgname=viennarna2
pkgver=2.1.8
pkgrel=1
pkgdesc="RNA Secondary Structure Prediction and Comparison"
arch=('i686' 'x86_64')
url="http://www.tbi.univie.ac.at/RNA/vrna2.html"
license=('custom')
depends=('gri' 'tcsh')
conflicts=('viennarna')
install='viennarna2.install'
source=("$pkgname-$pkgver.tar.gz::http://www.tbi.univie.ac.at/RNA/download.php?id=viennarna-$pkgver")
md5sums=('3e47c546857bb18a6f49271250cfde95')
options=(staticlibs)

build() {
  cd $srcdir/ViennaRNA-$pkgver

  ./configure --prefix=/usr --with-cluster

  make
}

check() {
  cd $srcdir/ViennaRNA-$pkgver

  make check
}

package() {
  cd $srcdir/ViennaRNA-$pkgver

  make DESTDIR=$pkgdir install

  install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/LICENSE

  # cleanup after dirty install ...
  for perlscript in $pkgdir/usr/share/ViennaRNA/bin/*.pl ; do
    mv $perlscript $pkgdir/usr/bin/$(basename $perlscript .pl)
  done
  mv $pkgdir/usr/share/ViennaRNA/bin/* $pkgdir/usr/bin
  rmdir $pkgdir/usr/share/ViennaRNA/bin
  rm -rf $pkgdir/usr/lib/perl5/core_perl

  # TODO swap static for shared libs
}
