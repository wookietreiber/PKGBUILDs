# Maintainer: Christian Krause ("wookietreiber") <kizkizzbangbang@googlemail.com>

_pkgname_base=revbayes
pkgname=$_pkgname_base-git

pkgver=r3178.2bf89b6
pkgrel=1
pkgdesc="bayesian phylogenetic inference using probabilistic graphical models and an interpreted language"
arch=(i686 x86_64)
url="https://github.com/revbayes/revbayes"
license=(custom)
depends=(boost-libs)
makedepends=(boost cmake git)

source=("$pkgname::git+https://github.com/revbayes/revbayes")

md5sums=('SKIP')

pkgver() {
  cd $srcdir/$pkgname

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $srcdir/$pkgname

  rm -rf boost_1_55_0

  mkdir build

  cd projects/cmake
  ./regenerate.sh -boost false
}

build() {
  cd $srcdir/$pkgname/build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    ../projects/cmake

  make
}

package() {
  cd $srcdir/$pkgname/build

  install -Dm755 rb          $pkgdir/usr/bin/rb
  install -Dm755 rb-extended $pkgdir/usr/bin/rb-extended

  mkdir -p $pkgdir/usr/share/licenses/$pkgname
  echo unknown > $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
