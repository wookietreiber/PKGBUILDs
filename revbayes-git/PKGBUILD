# Maintainer: Christian Krause ("wookietreiber") <kizkizzbangbang@googlemail.com>

_pkgname_base=revbayes
pkgname=$_pkgname_base-git

pkgver=r3984.a7b5a3c
pkgrel=1
pkgdesc="bayesian phylogenetic inference using probabilistic graphical models and an interpreted language"
arch=(i686 x86_64)
url="https://github.com/revbayes/revbayes"
license=(GPL3)
depends=(gcc-libs)
makedepends=(boost cmake git)

source=("$pkgname::git+https://github.com/revbayes/revbayes"
        'setprecision.patch')

md5sums=('SKIP'
         '517680950e5e598d5db3e295c4539a11')

pkgver() {
  cd $srcdir/$pkgname

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $srcdir/$pkgname

  rm -rf boost_1_55_0

  patch -Np1 -i $srcdir/setprecision.patch

  mkdir -p build

  cd projects/cmake
  ./regenerate.sh -boost false
}

build() {
  cd $srcdir/$pkgname/build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    ../projects/cmake

  make
}

package() {
  cd $srcdir/$pkgname/build

  install -Dm755 rb $pkgdir/usr/bin/rb
}
