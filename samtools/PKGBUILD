# Contributor: Markus Heuser <markus.heuser@web.de>
# Contributor: Christian Krause ("wookietreiber") <kizkizzbangbang@googlemail.com>
# Maintainer: Thiago Yukio Kikuchi Oliveira <stratust@gmail.com>

pkgname=samtools
pkgver=0.1.19
pkgrel=2
pkgdesc="Efficient utilities on manipulating alignments in the SAM format."
arch=("x86_64")
url="http://samtools.sourceforge.net/"
license=('MIT')
depends=('perl')
optdepends=('luajit: needed for r2plot.lua vcfutils.lua'
            'python2: needed for varfilter.py')
options=('staticlibs')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2)
md5sums=(ff8b46e6096cfb452003b1ec5091d86a)

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i 's?/software/bin/python?/usr/bin/python2?' misc/varfilter.py
  make CXXFLAGS=-fPIC CFLAGS=-fPIC CPPFLAGS=-fPIC
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -d "$pkgdir/usr/bin" "$pkgdir/usr/lib" "$pkgdir/usr/share/man/man1" "$pkgdir/usr/include/bam" "$pkgdir/usr/share/licenses/samtools/"

  install -m755 samtools "$pkgdir/usr/bin"

  for f in $(find bcftools/ -type f -executable) ; do
    install -m755 $f "$pkgdir/usr/bin"
  done

  for f in $(find misc/ -type f -executable) ; do
    install -m755 $f "$pkgdir/usr/bin"
  done

  install -m644 libbam.a "$pkgdir/usr/lib"

  install -m644 samtools.1 "$pkgdir/usr/share/man/man1"

  for f in *.h ; do
    install -m644 $f "$pkgdir/usr/include/bam"
  done

  install -m644 COPYING "$pkgdir/usr/share/licenses/samtools/LICENSE"
}
