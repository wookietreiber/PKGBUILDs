# Contributor: Markus Heuser <markus.heuser@web.de>
# Contributor: Christian Krause ("wookietreiber") <kizkizzbangbang@googlemail.com>
# Maintainer: Thiago Yukio Kikuchi Oliveira <stratust@gmail.com>

pkgname=samtools
pkgver=1.0
pkgrel=1
pkgdesc="tools for manipulating next-generation sequencing data"
arch=('i686' 'x86_64')
url="http://samtools.sourceforge.net/"
license=('custom')
depends=('perl' 'htslib')
optdepends=('luajit: needed for r2plot.lua vcfutils.lua'
            'python2: needed for varfilter.py')
options=('staticlibs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/samtools/samtools/archive/$pkgver.tar.gz"
        'system-htslib.patch')
md5sums=('7eff9d605f21b0910887424069bae72e'
         'SKIP')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 -i "$srcdir/system-htslib.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  make LDLIBS="-lhts"
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" prefix=/usr install

  install -Dm644 libbam.a "$pkgdir/usr/lib/libbam.a"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
