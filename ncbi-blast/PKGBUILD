# Maintainer: Christian Krause ("wookietreiber") <kizkizzbangbang@googlemail.com>
pkgname=ncbi-blast
pkgver=2.2.29
pkgrel=2
pkgdesc="finds regions of similarity between biological sequences"
arch=('i686' 'x86_64')
url="http://blast.ncbi.nlm.nih.gov/"
license=('unknown')
depends=('lzo2' 'pcre' 'python')
makedepends=('cpio')
provides=('blast')
conflicts=('blast+-bin')
replaces=('blast+-bin')
source=("ftp://ftp.ncbi.nlm.nih.gov/blast/executables/blast+/$pkgver/$pkgname-$pkgver+-src.tar.gz" 'typeinfo.patch')
md5sums=('1290360b24448dd79840a6800482b597' '9c257a762ce4e7418e95a48723832a86')

prepare() {
  cd "$srcdir/$pkgname-$pkgver+-src/c++"

  patch -p1 -i "$srcdir/typeinfo.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver+-src/c++"

  ./configure --prefix=/usr --with-dll --with-mt --without-gnutls
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver+-src/c++"

  make prefix="$pkgdir/usr/" install
  chmod +x $pkgdir/usr/lib/*.so
}
