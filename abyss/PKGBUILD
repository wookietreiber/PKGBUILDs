# Maintainer: Christian Krause ("wookietreiber") <kizkizzbangbang@googlemail.com>

pkgname=abyss
pkgver=1.9.0
pkgrel=1
pkgdesc="Assembly By Short Sequences - a de novo, parallel, paired-end sequence assembler"
arch=('i686' 'x86_64')
url="http://www.bcgsc.ca/platform/bioinfo/software/abyss"
license=('custom')
depends=('make' 'openmpi' 'sqlite3')
makedepends=('boost' 'sparsehash')
source=(https://github.com/bcgsc/abyss/releases/download/$pkgver/$pkgname-$pkgver.tar.gz)
md5sums=('7b1b9f060dbae6c7fe815b1e50487c83')

build() {
  cd $srcdir/$pkgname-$pkgver

  LDFLAGS+=" -L/usr/lib/openmpi"

  ./configure \
    --prefix=/usr \
    --with-boost=/usr/include/boost \
    --with-mpi=/usr

  make
}

check() {
  cd $srcdir/$pkgname-$pkgver

  make check
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR=$pkgdir install

  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
