# Maintainer: Christian Krause ("wookietreiber") <kizkizzbangbang@googlemail.com>

pkgname=picard-tools
pkgver=1.126
pkgrel=1
pkgdesc='set of tools for working with next generation sequencing data in the BAM format'
arch=('i686' 'x86_64')
url="https://github.com/broadinstitute/picard"
license=('GPL2')
depends=('bash' 'gcc-libs' 'java-runtime>=6')
source=("https://github.com/broadinstitute/picard/releases/download/$pkgver/$pkgname-$pkgver.zip"
        'picard.sh')
md5sums=('e455213f6b9f46433b7d59da78c5a0fb'
         'SKIP')

package() {
  cd $srcdir/$pkgname-$pkgver

  for f in *.jar ; do
    install -Dm755 $f $pkgdir/usr/share/java/picard-tools/$f
  done

  install -Dm755 libIntelDeflater.so $pkgdir/usr/lib/libIntelDeflater.so

  pushd $pkgdir/usr/share/java/picard-tools > /dev/null
  ln -s ../../../lib/libIntelDeflater.so
  popd > /dev/null

  install -Dm755 $srcdir/picard.sh $pkgdir/usr/bin/picard
}
